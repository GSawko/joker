(def namespaces ['string 'json 'base64 'os])

(apply require namespaces)

(def rpl joker.string/replace)

(def fn-template
  (slurp "fn.tmpl"))

(def package-template
  (slurp "package.tmpl"))

(def intern-template
  (slurp "intern.tmpl"))

(defn q
  [s]
  (str "\"" s "\""))

(defn go-name
  [fn-name]
  (let [n (-> fn-name
              (rpl "-" "_")
              (rpl "?" "")
              (str "_"))]
    (if (joker.string/ends-with? fn-name "?")
      (str "is" n)
      n)))

(defn extract-args
  [args]
  (joker.string/join
   "\n  "
   (map-indexed
    (fn [i arg]
      (let [m (meta arg)
            t (cond-> (:tag m)
                (:varargs m) (str "s"))]
        (str arg " := Extract" t "(args, " (str i) ")")))
    args)))

(defn handle-varargs
  [args]
  (let [c (count args)]
    (if (and (> c 1)
             (= '& (nth args (- c 2))))
      (let [vargs (vary-meta (last args) assoc :varargs true)]
        (conj (subvec args 0 (- c 2)) vargs))
      args)))

(defn generate-fn
  [ns-sym k v]
  (let [m (meta v)
        args (first (:arglists m))
        handle-args (handle-varargs args)
        cnt (count handle-args)
        varargs? (< cnt (count args))
        go-fn-name (go-name (str k))
        fn-str (-> fn-template
                   (rpl "{fnName}" go-fn-name)
                   (rpl "{minArity}" (str (if varargs? (dec cnt) cnt)))
                   (rpl "{maxArity}" (str (if varargs? 1000 cnt)))
                   (rpl "{args}" (extract-args handle-args))
                   (rpl "{goExpr}" (:go m))
                   (rpl "{return}"
                        (if (:tag m)
                          (str "return Make" (:tag m) "(res)")
                          "return res")))
        intern-str (-> intern-template
                       (rpl "{nsName}" (str ns-sym))
                       (rpl "{fnName}" (str k))
                       (rpl "{goName}" go-fn-name)
                       (rpl "{fnDocstring}" (:doc m))
                       (rpl "{added}" (:added m))
                       (rpl "{args}"
                            (joker.string/join ", " (for [arg args]
                                                      (str "MakeSymbol(" (q (str arg)) ")")))))]
    [fn-str intern-str]))

(defn generate-ns
  [ns-sym]
  (let [ns (find-ns ns-sym)
        m (meta ns)
        fns (for [[k v] (ns-publics ns)]
              (generate-fn ns-sym k v))
        res (-> package-template
                (rpl "{nsName}" (str ns-sym))
                (rpl "{imports}" (joker.string/join "\n  " (map q (:go-imports m))))
                (rpl "{fns}" (joker.string/join "\n" (map first fns)))
                (rpl "{nsDocstring}" (:doc m))
                (rpl "{interns}" (joker.string/join "\n" (map second fns))))]
    res))

(defn ns-file-name
  [ns-sym]
  (str ns-sym "/a_" ns-sym ".go"))

(doseq [ns-sym namespaces]
  (spit (ns-file-name ns-sym)
        (generate-ns ns-sym)))
